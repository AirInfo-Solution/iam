buildscript {
    repositories {
        jcenter()
        maven {
            url 'http://repo.spring.io/milestone'
        }
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.3.2.RELEASE")
        classpath("io.spring.gradle:dependency-management-plugin:0.5.3.RELEASE")
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.11'
}

allprojects {
    apply plugin: 'idea'
    apply plugin: 'eclipse'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'findbugs'
    apply plugin: 'jacoco'

    dependencyManagement {
        imports {
            mavenBom 'io.spring.platform:platform-bom:2.0.2.RELEASE'
        }
    }

    repositories {
        mavenCentral()
    }

    ext {
        generatedSourcesDir = new File("${projectDir}/src/generated")
        generatedSourcesJavaDir = new File(generatedSourcesDir, "/java")
    }

    sourceSets {
        main {
            java {
                srcDir generatedSourcesJavaDir
            }
        }
    }

    // See: http://csiebler.github.io/blog/2014/02/09/multi-project-code-coverage-using-gradle-and-jacoco/
    task unitTestCoverage(type: JacocoReport) {
        // (change this if you e.g. want to calculate unit test/integration test coverage separately)
        executionData file("build/jacoco/test.exec")

        // Add all relevant sourcesets from the subprojects
        sourceSets sourceSets.main

        reports {
            html.destination "${reporting.baseDir}/jacoco/unitTests"
            csv.enabled false
            xml.enabled false
        }
    }

    // always run the tests before generating the report
    unitTestCoverage.dependsOn test
    check.dependsOn unitTestCoverage

    findbugs {
        excludeFilter = new File(project.parent.projectDir, "codequality/findbugs/excludeFilter.xml")
    }

    tasks.withType(FindBugs) {
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }
}

idea {
    project {
        jdkName = '1.8'
        languageLevel = '1.8'
        vcs = 'Git'
    }
}